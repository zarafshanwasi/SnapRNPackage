buildscript {
  ext.getExtOrDefault = {name ->
    return rootProject.ext.has(name) ? rootProject.ext.get(name) : project.properties['ZmckitLibrary_' + name]
  }

  repositories {
    google()
    mavenCentral()
  }

  dependencies {
//    classpath("com.android.tools.build:gradle")
    // noinspection DifferentKotlinGradleVersion
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${getExtOrDefault('kotlinVersion')}"
  }
}

def isNewArchitectureEnabled() {
  return rootProject.hasProperty("newArchEnabled") && rootProject.getProperty("newArchEnabled") == "true"
}

apply plugin: "com.android.library"
apply plugin: "kotlin-android"

if (isNewArchitectureEnabled()) {
  apply plugin: "com.facebook.react"
}

def getExtOrIntegerDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : (project.properties["ZmckitLibrary_" + name]).toInteger()
}

static def supportsNamespace() {
  def parsed = com.android.Version.ANDROID_GRADLE_PLUGIN_VERSION.tokenize('.')
  def major = parsed[0].toInteger()
  def minor = parsed[1].toInteger()

  // Namespace support was added in 7.3.0
  return (major == 7 && minor >= 3) || major >= 8
}

android {
  if (supportsNamespace()) {
    namespace "com.zmckitlibrary"

//    sourceSets {
//      main {
//        manifest.srcFile "src/main/AndroidManifestNew.xml"
//      }
//    }
  }

  compileSdkVersion getExtOrIntegerDefault("compileSdkVersion")

  defaultConfig {
    minSdkVersion getExtOrIntegerDefault("minSdkVersion")
    targetSdkVersion getExtOrIntegerDefault("targetSdkVersion")
  }

  buildTypes {
    release {
      minifyEnabled false
    }
  }

  lintOptions {
    disable "GradleCompatible"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

repositories {
 mavenCentral()
 google()
}

def kotlin_version = getExtOrDefault("kotlinVersion")

dependencies {
  implementation "com.facebook.react:react-android"
  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

  implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
  implementation 'com.google.android.material:material:1.12.0'

  implementation 'androidx.recyclerview:recyclerview:1.4.0'

  def cameraKitVersion = '1.38.0'
  // Provides the core implementation of Camera Kit
  implementation "com.snap.camerakit:camerakit:$cameraKitVersion"
  implementation "com.snap.camerakit:support-camerax:$cameraKitVersion"
  implementation "com.snap.camerakit:support-snap-attribution:$cameraKitVersion"
  implementation "com.snap.camerakit:support-snap-button:$cameraKitVersion"
  implementation "com.snap.camerakit:support-permissions:$cameraKitVersion"

  implementation 'com.github.bumptech.glide:glide:4.16.0' // Latest stable version
}

